!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!    M O R P H O L O G I C A L · T R A N S D U C E R · F O R · N O G A Y    !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! See http://wiki.apertium.org/wiki/Turkic_lexicon

Multichar_Symbols

%<n%>                     ! 
%<v%>                     ! 
%<np%>                    ! 
%<num%>                   ! 
%<adj%>                   ! 
%<det%>                   ! 
%<prn%>                   ! 
%<adv%>                   ! 
%<post%>                  ! 
%<cnjsub%>                ! 
%<cnjcoo%>                ! 

%<attr%>                  ! 
%<subst%>                 ! 
%<advl%>                  ! 

%<top%>                   ! 
%<ant%>                   ! 
%<pers%>                  ! 

%<ind%>                   ! 
%<dem%>                   ! 
%<itg%>                   ! 
%<qnt%>                   ! 
%<ord%>                   ! 
%<ref%>                   ! 

%<m%>                     ! 
%<f%>                     ! 

%<sg%>                    ! 
%<pl%>                    ! 

%<p1%>                    ! 
%<p2%>                    ! 
%<p3%>                    ! 

%<px1sg%>                 ! 
%<px2sg%>                 ! 
%<px3sp%>                 ! 
%<px1pl%>                 ! 
%<px2pl%>                 ! 
%<px3pl%>                 ! 

%<nom%>                   ! 
%<gen%>                   ! 
%<dat%>                   ! 
%<acc%>                   ! 
%<loc%>                   ! 
%<abl%>                   ! 

%<sim%>                   ! 

%<iv%>                    ! 
%<tv%>                    ! 

%<neg%>                   ! 

%<fut%>                   ! 
%<pres%>                  ! 
%<past%>                  ! 
%<ifi%>                   ! 

%<ger%>                   ! 

%<prc_perf%>              ! 
%<prc_cond%>              ! 
%<gna_perf%>              ! 
%<gna_cond%>              ! 

! Punctuation
%<sent%>                  ! Sentence marker
%<guio%>                  ! Hyphen
%<cm%>                    ! Comma
%<apos%>                  ! Apostrophe
%<rquot%>                 ! Quote marker (right hand side)
%<lquot%>                 ! Quote marker (left hand side)
%<rpar%>                  ! Parenthetical marker (right hand side)
%<lpar%>                  ! Parenthetical marker (left hand side)

! Archiphonemes and escaped symbols
%{A%} 
%{I%}
%{G%}
%{U%}
%{D²%}
%{D³%}
%{M%}
%{L%}
%{n%}
%{s%}
%{u%}
%{ь%}

%> 
% 
%~
%-

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Nouns ; 
Postpositions ; 
Numerals ; 
Determiners ; 
Pronouns ; 
Conjunctions ; 
Adverbs ; 
Adjectives ; 
ProperNouns ; 
Verbs ; 
Punctuation ; 

LEXICON CLIT 

# ;

LEXICON CASES 

%<nom%>: CLIT ; 
%<gen%>:%>%{D³%}%{I%}нъ CLIT ; 
%<acc%>:%>%{D²%}%{I%} CLIT ; 
%<dat%>:%>%{G%}%{A%} CLIT ; 
%<loc%>:%>%{D²%}%{A%} CLIT ; 
%<abl%>:%>%{D³%}%{A%}н CLIT ; 

LEXICON ATTR-SUBST

%<attr%>: # ;
%<subst%>: CASES ;
%<subst%>%<pl%>:%>%{L%}%{A%}р CASES ;

LEXICON GENPOSS-ETC 

%<loc%>:%>%{D²%}%{A%}г%{I%} ATTR-SUBST ;
%<sim%>:%>%{D²%}%{A%}й CLIT ;

LEXICON CASES-ETC

CASES ;
GENPOSS-ETC ;

LEXICON POSS

CASES-ETC ; 
%<px1sg%>:%>%{I%}м CASES-ETC ; 
%<px2sg%>:%>%{I%}нъ CASES-ETC ; 
%<px3sp%>:%>%{s%}%{I%}%{n%} CASES-ETC ; 
%<px1pl%>:%>%{I%}м%{I%}з CASES-ETC ; 
%<px2pl%>:%>%{I%}нъ%{I%}з CASES-ETC ; 
%<px3pl%>:%>%{L%}%{A%}р%{I%} CASES-ETC ; 

LEXICON N-INFL-COMMON

POSS ;
CASES-ETC ;

LEXICON NP-COMMON
N-INFL-COMMON ;

LEXICON SUBST

N-INFL-COMMON ;
%<pl%>:%>%{L%}%{A%}р N-INFL-COMMON ;

LEXICON N-COMPOUND-PX-COMMON

:%>%{S%}%{I%}%{n%} CASES-ETC ;
POSS ;   

LEXICON N1 

%<n%>%<attr%>: # ;
%<n%>: SUBST ;

LEXICON N2

%<n%>: N-COMPOUND-PX-COMMON ;
%<n%>%<pl%>:%>%{L%}%{A%}р%>%{S%}%{I%}%{n%} CASES-ETC ; ! (e.g. чик сакчылары=пограничники)

LEXICON NP-TOP-ACR

%<np%>%<top%>:%- CASES ;
%<np%>%<top%>%<attr%>: # ;

LEXICON NP-TOP

%<np%>%<top%>: NP-COMMON ;
%<np%>%<top%>%<attr%>: # ;

LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: NP-COMMON ;
%<np%>%<ant%>%<m%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: NP-COMMON ;
%<np%>%<ant%>%<f%>%<pl%>:%>%{L%}%{A%}р NP-COMMON ;

LEXICON NP-AL

%<np%>%<al%>: CASES ;

LEXICON A1 

%<adj%>: # ; 
%<adj%>%<subst%>: SUBST ;
! TODO: Comparison

LEXICON A3

%<adj%>: # ; 
%<adj%>%<subst%>: SUBST ;

LEXICON A4

%<adj%>: # ;

LEXICON V-PERS-S1-NO3PERSON

%<p1%>%<sg%>:%>%{M%}%{A%}н CLIT ;
%<p2%>%<sg%>:%>с%{I%}нъ CLIT ;
%<p1%>%<pl%>:%>%{M%}%{I%}з CLIT ;
%<p2%>%<pl%>:%>с%{I%}з CLIT ;

LEXICON V-PERS-S1 

V-PERS-S1-NO3PERSON ;
%<p3%>%<sg%>: CLIT  ; 
%<p3%>%<pl%>: CLIT ;

LEXICON V-PERS-S2

%<p1%>%<sg%>:%>м CLIT ;
%<p2%>%<sg%>:%>нъ CLIT ;
%<p3%>%<sg%>: CLIT ;
%<p1%>%<pl%>:%>к CLIT ;
%<p2%>%<pl%>:%>%{I%}нъ%{I%}з CLIT ;
%<p3%>%<pl%>: CLIT ;
%<p3%>%<pl%>:%>%{L%}%{A%}р CLIT ;

LEXICON V-FIN-REG-NEG

%<past%>:%>%{G%}%{A%}н V-PERS-S1 ;
%<ifi%>:%>%{D²%}%{I%} V-PERS-S2 ;

LEXICON V-FIN-IRREG-NEG

%<pres%>:%>%{A%} V-PERS-S1 ; ! FIXME
%<neg%>%<pres%>:%>%{M%}%{A%}й V-PERS-S1 ; ! FIXME

%<fut%>:%>%{A%}р V-PERS-S1 ;
%<neg%>%<fut%>:%>м%{A%} V-PERS-S1 ;

LEXICON V-NONFIN-IRREG-NEG 

%<prc_perf%>:%>%{I%}п CLIT ; 
%<neg%>%<prc_perf%>:%>%{M%}%{A%}%>й CLIT ;

%<gna_perf%>:%>%{I%}п CLIT ;
%<neg%>%<gna_perf%>:%>%{M%}%{A%}%>й CLIT ;

LEXICON V-NONFIN-REG-NEG

%<ger%>:%>%{u%}%{ь%}в CASES ;  ! Also takes possessives

%<prc_cond%>:%>с%{A%} CLIT ; 

%<gna_cond%>:%>с%{A%} CLIT ;

LEXICON V-COMMON

V-FIN-REG-NEG ; 
%<neg%>:%>%{M%}%{A%} V-FIN-REG-NEG ; 

V-FIN-IRREG-NEG ; 

V-NONFIN-REG-NEG ; 
%<neg%>:%>%{M%}%{A%} V-NONFIN-REG-NEG ;

V-NONFIN-IRREG-NEG ; 


LEXICON V-IV 

%<v%>%<iv%>: V-COMMON ; 

LEXICON V-TV

%<v%>%<tv%>: V-COMMON ; 



LEXICON PRON-MEN

%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:ен # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:еним # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<dat%>:ага # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:ени # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:енде # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:еннен # ;

LEXICON PRON-SEN

%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:ен # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:енинъ # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<dat%>:ага # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:ени # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:енде # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:еннен # ;

LEXICON PRON-OL

%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:л # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:нынъ # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:га # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:ны # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:нда # ;
%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:ннан # ;

LEXICON PRON-BIZ 

%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>: # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:им # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<dat%>:ге # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:де # ;

LEXICON PRON-SIZ 

%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>: # ;
%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:инъ # ;
%<prn%>%<pers%>%<p2%>%<pl%>%<dat%>:ге # ;
%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:де # ;

LEXICON PRON-OLAR 

%<prn%>%<pers%>%<p3%>%<pl%>: CASES ;

LEXICON PRON-DEM-BU

бу%<prn%>%<dem%>%<nom%>:бу CLIT ;
бу%<prn%>%<dem%>%<gen%>:мунынъ CLIT ;
бу%<prn%>%<dem%>%<dat%>:буга CLIT ;
бу%<prn%>%<dem%>%<acc%>:муны CLIT ;
бу%<prn%>%<dem%>%<loc%>:мунда CLIT ;
бу%<prn%>%<dem%>%<abl%>:мундан CLIT ;
бу%<prn%>%<dem%>%<pl%>:булар CASES-ETC ;
бу%<prn%>%<dem%>%<sim%>:мундай CLIT ;

LEXICON PRON-DEM-SOL

%<prn%>%<dem%>%<nom%>:л CLIT ;
%<prn%>%<dem%>%<gen%>:нынъ CLIT ;
%<prn%>%<dem%>%<dat%>:нга CLIT ;
%<prn%>%<dem%>%<acc%>:ны CLIT ;
%<prn%>%<dem%>%<loc%>:нда CLIT ;
%<prn%>%<dem%>%<abl%>:ннан CLIT ;
%<prn%>%<dem%>%<pl%>:лар CASES-ETC ;
%<prn%>%<dem%>%<sim%>:ндай CLIT ;

LEXICON PRON-ITG-QAJSY

%<prn%>%<itg%>%<nom%>: CLIT ; 

LEXICON PRON-ITG 

%<prn%>%<itg%>: SUBST ; 

LEXICON PRON-NEG

%<prn%>%<neg%>: SUBST ; 

LEXICON PRON-QNT-BERI

%<prn%>%<qnt%>: CASES ; 

LEXICON NUM-COMMON

%<num%>: # ;
%<num%>%<subst%>: SUBST ;

LEXICON NUM-ROMAN

%<num%>%<ord%>: # ;

LEXICON NUM

NUM-COMMON ;
%<num%>%<ord%>:%>%{I%}нш%{I%} # ;             ! FIXME: base form, <det> reading
%<num%>%<ord%>:%>%{I%}нш%{I%} N-INFL-COMMON ;

LEXICON DET-DEM

%<det%>%<dem%>: # ;

LEXICON DET-REF

%<det%>%<ref%>: # ;

LEXICON DET-QNT

%<det%>%<qnt%>: # ;



LEXICON CC

%<cnjcoo%>: # ; 

LEXICON CS

%<cnjsub%>: # ; 

LEXICON ADV1

%<adv%>: # ; 

LEXICON ADV2

N1;
ADV1 ; 

LEXICON ADV3

%<adv%>%<itg%>: # ; 


LEXICON POST 

%<post%>: # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Postpositions

уьшин:уьшин POST ; ! "because of"
карап:карап POST ; ! ""
баслап:баслап POST ; ! ""
бербетин:бербетин POST ; ! ""
дейим:дейим POST ; ! ""
сайын:сайын POST ; ! ""
бойынша:бойынша POST ; ! ""

ман:%~ман POST ; ! "" ! Dir/RL
ман:мен POST ; ! "" Dir/LR
ман:ман POST ; ! "" Dir/LR
ман:пен POST ; ! "" Dir/LR
ман:пан POST ; ! "" Dir/LR

LEXICON Conjunctions 

да:%~да CC ; ! "" Dir/RL
да:да CC ; ! ""       Dir/LR
да:де CC ; ! ""       Dir/LR
да:та CC ; ! ""       Dir/LR
да:те CC ; ! ""       Dir/LR

не:не CC ; ! "neither... , nor ... (x2)" ! FIXME:NOTE: disambig
тек:тек CC ; ! "но"
я:я CC ; ! "или"
яде:яде CC ; ! ""
ама:ама CC ; ! "но"
эм:эм CC ; ! "и"

эгер:эгер CS ; ! "если"

LEXICON Pronouns 

мен:м PRON-MEN ; ! "я"
сен:с PRON-SEN ; ! "ты"
ол:о PRON-OL ;  ! "он; она; оно"
биз:биз PRON-BIZ ; ! "мы"
сиз:сиз PRON-SIZ ; ! "вы"
олар:олар PRON-OLAR ; ! "они"

PRON-DEM-BU ; ! ""

ол:о PRON-DEM-SOL ; ! ""
сол:со PRON-DEM-SOL ; ! ""

ким:ким PRON-ITG ; ! "кто"
не:не PRON-ITG ; ! "что"
кайсы:кайсы PRON-ITG-QAJSY ; ! ""

еш% ким:еш% ким PRON-NEG ; ! ""

баьри:баьри PRON-QNT-BERI ; ! ""

LEXICON Determiners

бу:бу DET-DEM ; ! ""
ол:ол DET-DEM ; ! ""
сол:сол DET-DEM ; ! ""

оьз:оьз DET-REF ; ! "oneself's" FIXME

аьр:аьр DET-QNT ; ! "каждый"


LEXICON Numerals

бир:бир NUM ; ! "один"
эки:эки NUM ; ! ""
уьш:уьш NUM ; ! ""
доьрт:доьрт NUM ; ! ""
бес:бес NUM ; ! ""
алты:алты NUM ; ! ""
йети:йети NUM ; ! ""
сегиз:сегиз NUM ; ! ""
тогыз:тогыз NUM ; ! ""
он:он NUM ; ! ""

йырма:йырма NUM ; ! ""
отыз:отыз NUM ; 
кырк:кырк NUM ; ! ""
элли:элли NUM ; ! ""
алпыс:алпыс NUM ; ! ""
етпис:етпис NUM ; ! ""
сексен:сексен NUM ; ! ""
токсан:токсан NUM ; ! ""

юз:юз NUM ; ! ""

мынъ:мынъ NUM ; ! "тысыч"

I NUM-ROMAN ; ! ""
II NUM-ROMAN ; ! ""
III NUM-ROMAN ; ! ""
IV NUM-ROMAN ; ! ""
V NUM-ROMAN ; ! ""
VI NUM-ROMAN ; ! ""
VII NUM-ROMAN ; ! ""
VIII NUM-ROMAN ; ! ""
IX NUM-ROMAN ; ! ""
X NUM-ROMAN ; ! ""
XI NUM-ROMAN ; ! ""
XII NUM-ROMAN ; ! ""
XIII NUM-ROMAN ; ! ""
XIV NUM-ROMAN ; ! ""
XV NUM-ROMAN ; ! ""
XVI NUM-ROMAN ; ! ""
XVII NUM-ROMAN ; ! ""
XVIII NUM-ROMAN ; ! ""
XIX NUM-ROMAN ; ! ""
XX NUM-ROMAN ; ! ""
XXI NUM-ROMAN ; ! ""


LEXICON Nouns

иш:иш N1 ; ! ""
алд:алд N1 ; ! ""
ара:ара N1 ; ! ""
уьст:уьст N1 ; ! ""

орын:орын N1 ; ! "место"
ер:ер N1 ; ! "место"
агаш:агаш N1 ; ! "дерево"
ана:ана N1 ; ! "мать"
ат:ат N1 ; ! ""
ата:ата N1 ; ! "отец"
аьел:аьел N1 ; ! "семья"
аьрекет:аьрекет N1 ; ! ""
бала:бала N1 ; ! ""
бас:бас N1 ; ! "голов"
боьри:боьри N1 ; ! "волк"
дос:дос N1 ; ! "друг"
ел:ел N1 ; ! "" 
ие:ие N1 ; ! "" 
йыл:йыл N1 ; ! "год"
кезуьв:кезуьв N1 ; ! "период" 
клуб:клуб N1 ; ! "клуб"
коян:коян N1 ; ! "заяц"
кум:кум N1 ; ! "песок"
куьн:куьн N1 ; ! "день; солнце"
кыз:кыз N1 ; ! "девушка" 
кыс:кыс N1 ; ! "зима"
оьсимлик:оьсимлик N1 ; ! ""
соьз:соьз N1 ; ! "слово"
сув:сув N1 ; ! "вода"
суьек:суьек N1 ; ! ""
тавшан:тавшан N1 ; ! ""
тамшы:тамшы N1 ; ! ""
тас:тас N1 ; ! 'камен'
терек:терек N1 ; ! "дерево" 
туьлки:туьлки N1 ; ! ""
туьн:туьн N1 ; ! "ночь"
уьй:уьй N1 ; ! "дом"
юлдыз:юлдыз N1 ; ! "звезд"
ямгыр:ямгыр N1 ; ! "дожд"
япырак:япырак N1 ; ! "лист"
ер:ер N1 ; ! "земля"
ис:ис N1 ; ! "работа"
йол:йол N1 ; ! "дорога"
киси:киси N1 ; ! "человек"
мектеб:мектеб N1 ; ! "школа"
халк:халк N1 ; ! ""
аьдем:аьдем N1 ; ! "человек"
миллет:миллет N1 ; ! ""
тил:тил N1 ; ! "язык"
китап:китап N1 ; ! "книга"
керек:керек N1 ; ! ""
соьзлик:соьзлик N1 ; ! "словарь"
тарых:тарых N1 ; ! ""
сорав:сорав N1 ; ! "вопрос"
вакыт:вакыт N1 ; ! "время"
заман:заман N1 ; ! "время"
ярдам:ярдам  N1 ; ! "помощь"
дем:дем N1 ; ! "помощь"
колкабыс:колкабыс N1 ; ! "помощь"
коьмек:коьмек N1 ; ! "помощь"
ой:ой N1 ; ! "идея, затея"
согыс:согыс N1 ; ! "война"
кавга:кавга N1 ; ! "война"
согысшы:согысшы N1 ; ! "воин"
коьпир:коьпир N1 ; ! "мост"
авыл:авыл N1 ; ! "кишлак"
кыслав:кыслав N1 ; ! "кишлак"
мырат:мырат N1 ; ! ""
ниет:ниет N1 ; ! ""
аьл:аьл N1 ; ! ""
дин:дин N1 ; ! ""
эл:эл N1 ; ! ""
бурын:бурын N1 ; ! "нос"



ногай:ногай N1 ; ! ""
орыс:орыс N1 ; ! ""
абаза:абаза N1 ; ! ""
шеркеш:шеркеш N1 ; ! ""
карашай:карашай N1 ; ! ""


доклад:доклад N1 ; ! "доклад"
съезд:съезд N1 ; ! ""
район:район N1 ; ! "район"
документация:документация N1 ; ! ""
республика:республика N1 ; ! ""
орган:орган N1 ; ! ""
бюджет:бюджет N1 ; ! ""
регион:регион N1 ; ! ""
программа:программа N1 ; ! ""
проект:проект N1 ; ! ""
президент:президент N1 ; ! ""
министерство:министерство N1 ; ! ""
делегация:делегация N1 ; ! ""
пакет:пакет N1 ; ! ""
план:план N1 ; ! ""
бизнес%-план:бизнес%-план N1 ; ! ""
инвестор:инвестор N1 ; ! ""
ведомство:ведомство N1 ; ! ""
технология:технология N1 ; ! ""
школа:школа N1 ; ! ""
институт:институт N1 ; ! ""
процент:процент N1 ; ! ""
администрация:администрация N1 ; ! ""
фотоаппарат:фотоаппарат N1 ; ! ""
конкурс:конкурс N1 ; ! ""
автобус:автобус N1 ; ! ""
асфальт:асфальт N1 ; ! ""
бизнес:бизнес N1 ; ! ""
бюро:бюро N1 ; ! ""
газ:газ N1 ; ! ""
газета:газета N1 ; ! ""
гектар:гектар N1 ; ! ""
депутат:депутат N1 ; ! ""
директор:директор N1 ; ! ""
история:история N1 ; ! ""
интернет:интернет N1 ; ! ""
класс:класс N1 ; ! ""
комиссия:комиссия N1 ; ! ""
компьютер:компьютер N1 ; ! ""
конкурс:конкурс N1 ; ! ""
кризис:кризис N1 ; ! ""
машина:машина N1 ; ! ""
механика:механика N1 ; ! ""
министр:министр N1 ; ! ""
партия:партия N1 ; ! ""
промышленность:промышленность N1 ; ! ""
пункт:пункт N1 ; ! ""
совет:совет N1 ; ! ""
спорт:спорт N1 ; ! ""
статистика:статистика N1 ; ! ""
телефон:телефон N1 ; ! ""
техника:техника N1 ; ! ""
трактор:трактор N1 ; ! ""
федерация:федерация N1 ; ! ""
ферма:ферма N1 ; ! ""
филиал:филиал N1 ; ! ""
финанс:финанс N1 ; ! ""
фирма:фирма N1 ; ! ""
фольклор:фольклор N1 ; ! ""
химик:химик N1 ; ! ""
штат:штат N1 ; ! ""
энергетика:энергетика N1 ; ! ""
управление:управление N1 ; ! ""
объект:объект N1 ; ! ""
приоритет:приоритет N1 ; ! ""
политика:политика N1 ; ! ""
автор:автор N1 ; ! ""
закон:закон N1 ; ! ""
пресс%-конференция:пресс%-конференция N1 ; ! ""
конференция:конференция N1 ; ! ""
субъект:субъект N1 ; ! ""
информация:информация N1 ; ! ""
министр:министр N1 ; ! ""
культура:культура N1 ; ! ""
литература:литература N1 ; ! ""
температура:температура N1 ; ! ""
инфраструктура:инфраструктура N1 ; ! ""
структура:структура N1 ; ! ""	
конфессия:конфессия N1 ; ! ""
чиновник:чиновник N1 ; ! ""
делегат:делегат N1 ; ! ""
документ:документ N1 ; ! ""
терроризм:терроризм N1 ; ! ""
террорист:террорист N1 ; ! ""
экстремизм:экстремизм N1 ; ! ""
газета:газета N1 ; ! ""
райадминистрация:райадминистрация N1 ; ! ""
администрация:администрация N1 ; ! ""
шахматист:шахматист N1 ; ! ""
техуниверситет:техуниверситет N1 ; ! ""
университет:университет N1 ; ! ""
налог:налог N1 ; ! ""
граждан:граждан N1 ; ! ""
книга:книга N1 ; ! "книга"
идея:идея N1 ; ! ""
хозяйство:хозяйство N1 ; ! ""
милиция:милиция N1 ; ! ""
совет:совет N1 ; ! ""

председатель:председатель N1 ; ! ""
исследователь:исследователь N1 ; ! ""
область:область N1 ; ! ""
власть:власть N1 ; ! ""

февраль:февраль N1 ; ! ""
декабрь:декабрь N1 ; ! ""
октябрь:октябрь N1 ; ! ""

куьн% аьли:куьн% аьль N2 ; ! ""

LEXICON ProperNouns

Дагестан:Дагестан NP-TOP ; ! ""
Россия:Россия NP-TOP ; ! ""
Америка:Америка NP-TOP ; ! ""
Нью%-Джерси:Нью%-Джерси NP-TOP ; ! ""
Нью%-Йорк:Нью%-Йорк NP-TOP ; ! ""
Санкт%-Петербург:Санкт%-Петербург NP-TOP ; ! ""
Москва:Москва NP-TOP ; ! ""

ДР:ДР NP-TOP-ACR ; ! ""
РФ:РФ NP-TOP-ACR ; ! ""

Азамат:Азамат NP-ANT-M ; ! ""
Марат:Марат NP-ANT-M ; ! ""
Андрей:Андрей NP-ANT-M ; ! ""
Александр:Александр NP-ANT-M ; ! ""
Сергей:Сергей NP-ANT-M ; ! ""
Владимир:Владимир NP-ANT-M; ! ""
Магомедсалам:Магомедсалам NP-ANT-M ; ! ""
Иван:Иван NP-ANT-M ; ! ""
Том:Том NP-ANT-M ; ! "Tom"
Хосе:Хосе NP-ANT-M ; ! ""
Борис:Борис NP-ANT-M ; ! ""

Катя:Катя NP-ANT-F ; ! ""
Ольга:Ольга NP-ANT-F ; ! ""
Танья:Танья NP-ANT-F ; ! ""
Наталья:Наталья NP-ANT-F ; ! ""

Единая% Россия:Единая% Россия NP-AL ; ! ""


LEXICON Adjectives

йок:йок A3 ; ! ""
бар:бар A3 ; ! ""
коьп:коьп A1 ; ! "много"

кызыл:кызыл A1 ; ! "красный"
уьйкен:уьйкен A1 ; ! "большой"
яс:яс A1 ; ! ""
керек:керек A1 ; ! ""
тез:тез A1 ; ! ""
артык:артык A1 ; ! "лишний, особенный" 
тыныш:тыныш A1 ; ! "мирный"
тынышлык:тынышлык A1 ; ! "мирный"
дурыс:дурыс A1 ; ! ""
ортак:ортак A1 ; ! ""
яман:яман A1 ; ! "плохой"
ашык:ашык A1 ; ! "красочный"
янъы:янъы A1 ; ! "новый"


социал%-экономикалык:социал%-экономикалык A4 ; ! ""
экономикалык:экономикалык A4 ; ! ""
политикалык:политикалык A4 ; ! ""
социаллык:социаллык A4 ; ! ""
муниципаллык:муниципаллык A4 ; ! ""
регионаллык:регионаллык A4 ; ! ""
федераллык:федераллык A4 ; ! ""
республикалык:республикалык A4 ; ! ""	
савлайроссиялык:савлайроссиялык A4 ; ! ""	

общий:общий A4 ; ! ""
локальный:локальный A4 ; ! ""

ерли:ерли A4 ; ! ""
орынлы:орынлы A4 ; ! ""


LEXICON Adverbs 

калай:калай ADV3 ; ! "как"
кайда:кайда ADV3 ; ! "где"
кайерде:кайерде ADV3 ; ! "где"
кайдан:кайдан ADV3 ; ! "откуда"
кайерден:кайерден ADV3 ; ! "откуда"

бек:бек ADV1 ; ! "очень"
энъ:энъ ADV1 ; ! "" 
айлак:айлак ADV1 ; ! ""
айырым:айырым ADV1 ; ! "врозь"
сонъ:сонъ ADV1 ; ! "потом"

орысша:орысша ADV2 ; ! ""
ногайша:ногайша ADV2 ; ! ""
абазаша:абазаша ADV2 ; ! ""
карашайша:карашайша ADV2 ; ! ""
шеркешше:шеркешше ADV2 ; ! ""


LEXICON Verbs 

бол:бол V-IV ; ! "быть"

тур:тур V-IV ; ! ""
бер:бер V-TV ; ! ""
ят:ят V-IV ; ! ""
кой:кой V-TV ; ! ""
шык:шык V-IV ; ! ""
йибер:йибер V-TV ; ! ""

кел:кел V-IV ; ! ""
де:де V-TV ; ! ""
соьйле:соьйле V-TV ; ! ""
ал:ал V-TV ; ! ""
эт:эт V-TV ; ! ""
яша:яша V-IV ; ! "жить"
тув:тув V-IV ; ! "восходить"
шыг:шыг V-IV ; ! "восходить"
сора:сора V-TV ; ! "просить"
сакла:сакла V-TV ; ! "защищать"
оьт:оьт V-TV ; ! "проникать"
озгар:озгар V-TV ; ! ""
яз:яз V-TV ; ! "писать"
белгиле:белгиле V-TV ; ! "" FIXME
айт:айт V-TV ; ! ""

LEXICON Punctuation

%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;



